DefineConstant
[
  inf_width = {0.2, Name "inflow width"}
  step_height = {0.1, Name "step height"}
  step_dist = {1.0, Name "step distance"}
  top_len = {3.0, Name "top length"}
  control_width = {0.02, Name "control width"}
  refined_size = {0.01, Name "size for step"}
  coarse_size= {0.02, Name "size coarse"}
];

// length_bef_control = step_dist - control_width;
outflow_width = step_height + inf_width;
bott_len = top_len - step_dist;

p = newp;

Point(p+1) = {0, step_height, 0, coarse_size};
// Point(p+2) = {length_bef_control, step_height, 0, refined_size};
Point(p+2) = {step_dist, step_height, 0, refined_size};
Point(p+3) = {step_dist, control_width, 0, refined_size};
Point(p+4) = {step_dist, 0, 0, refined_size};
Point(p+5) = {top_len, 0, 0, coarse_size};
Point(p+6) = {top_len, outflow_width, 0, coarse_size};
Point(p+7) = {0, outflow_width, 0, coarse_size};

l = newl;

Line(l+1) = {p+1, p+2};
Line(l+2) = {p+2, p+3};
Line(l+3) = {p+3, p+4};
Line(l+4) = {p+4, p+5};
Line(l+5) = {p+5, p+6};
Line(l+6) = {p+6, p+7};
Line(l+7) = {p+7, p+1};

// Outflow
out = 2;
Physical Line(out) = {l+5};

// No slip wall
noslip1=3;
noslip2=4;
noslip3=5;
Physical Line(noslip1) = {l+1, l+2};
Physical Line(noslip2) = {l+4};
Physical Line(noslip3) = {l+6};

// Inlet
in=1;
Physical Line(in) = {l+7};

// control line
control=6;
Physical Line(control) = {l+3};

bd = newll; 
Curve Loop(bd) = {(l+1), (l+2), (l+3), (l+4), (l+5), (l+6), (l+7)}; 

s = news;

Plane Surface(s) = {bd};
tot=1;
Physical Surface(tot) = {s};

